pluginManagement {
  repositories {
    gradlePluginPortal()
    google()
    mavenCentral()
    maven { url 'https://jitpack.io' }
    mavenLocal()
    maven { url 'https://maven.aliyun.com/nexus/content/groups/public/' }
  }
}

enableFeaturePreview('VERSION_CATALOGS')
dependencyResolutionManagement {
  //  repositoriesMode.set(RepositoriesMode.FAIL_ON_PROJECT_REPOS)
  repositories {
    google()
    mavenCentral()
    mavenLocal()
    maven { url 'https://maven.aliyun.com/nexus/content/groups/public/' }
    maven { url 'https://jitpack.io' }
    maven {
      url 'https://pkgs.dev.azure.com/MicrosoftDeviceSDK/DuoSDK-Public/_packaging/Duo-SDK-Feed/maven/v1'
    }
  }

  versionCatalogs {
    libs {
      from(files('libs.versions.toml'))
    }
  }
}

gradle.rootProject {
  it.subprojects { sub ->
    beforeEvaluate {
      print(sub.name)
      switch (sub.name) {
        case "app":
          break
        case "AriaAnnotations":
        case "AriaCompiler":
          sub.apply {
            from "${project.rootDir.path}/javaComponent.gradle"
          }
          break
        default:
          sub.apply {
            from "${project.rootDir.path}/androidComponent.gradle"
          }
          sub.android.namespace = "com.lyy.dua.${sub.name}"
          break
      }
    }
  }
}

def componentList = [':app', ':Aria', ':AriaAnnotations', ':AriaCompiler',
    ':AppFrame', ':Http', ':M3U8Component', ':SFtpComponent', ':FtpComponent', ':PublicComponent',
    ':Queue', ':Schedulers'
]

componentList.forEach {
  include(it)
}
rootProject.name = 'Aria'
